@echo off
setlocal
chcp 65001 >nul
title Elden Ring Live Map - setup

cd /d "%~dp0"

rem ---------------------------------------------------------------------------
rem Your Elden Ring install is found automatically by scanning your Steam
rem libraries. Only fill this in if that fails - it must be the folder that
rem contains eldenring.exe and regulation.bin.
rem
rem   set GAMEDIR=D:\Games\Steam\steamapps\common\ELDEN RING\Game
rem   set MODDIR=D:\Games\ELDEN RING Reforged\mod
rem ---------------------------------------------------------------------------
if not defined GAMEDIR set "GAMEDIR="
if not defined MODDIR set "MODDIR="
if not "%MODDIR%"=="" set "ER_MOD_DIR=%MODDIR%"

echo.
echo   Elden Ring Live Map - setup
echo   ===========================
echo.

where python >nul 2>nul
if errorlevel 1 goto :no_python

echo   [1/6] Installing Python packages ...
python -m pip install --quiet --disable-pip-version-check zstandard pycryptodome pillow texture2ddecoder numpy
if errorlevel 1 goto :pip_failed

echo   [2/6] Extracting map tiles from your game ^(a couple of minutes^) ...
if "%GAMEDIR%"=="" python tools\extract_tiles.py
if not "%GAMEDIR%"=="" python tools\extract_tiles.py --game-dir "%GAMEDIR%"
if errorlevel 1 goto :extract_failed

echo   [3/6] Building the marker dataset ...
if "%GAMEDIR%"=="" python tools\build_markers.py
if not "%GAMEDIR%"=="" python tools\build_markers.py "%GAMEDIR%"
if errorlevel 1 goto :markers_failed

echo   [4/6] Indexing the game's map files ...
python tools\enumerate_maps.py >nul
if errorlevel 1 goto :items_failed

echo   [5/6] Extracting item locations ^(this reads 864 map files^) ...
if "%GAMEDIR%"=="" python tools\extract_items.py
if not "%GAMEDIR%"=="" python tools\extract_items.py --game-dir "%GAMEDIR%"
if errorlevel 1 goto :items_failed

echo   [6/6] Extracting the game's map icons ...
if "%GAMEDIR%"=="" python tools\extract_icons.py
if not "%GAMEDIR%"=="" python tools\extract_icons.py --game-dir "%GAMEDIR%"
if errorlevel 1 goto :icons_failed

echo.
echo   Done. Start it any time with "Start Map.bat".
echo.
pause
goto :eof

:no_python
echo   Python not found. Install it from https://python.org, then run this again.
echo   Tick "Add Python to PATH" in the installer, and open a NEW window afterwards.
echo.
pause & goto :eof

:pip_failed
echo.
echo   Installing the Python packages failed. Try it by hand to see the error:
echo     python -m pip install zstandard pycryptodome pillow texture2ddecoder numpy
echo.
pause & goto :eof

:extract_failed
echo.
echo   Tile extraction failed.
echo   If it could not find your game, set GAMEDIR at the top of this file to the
echo   folder containing eldenring.exe and regulation.bin, then run this again.
echo.
pause & goto :eof

:markers_failed
echo.
echo   Building the marker dataset failed. See the message above.
echo.
pause & goto :eof

:icons_failed
echo.
echo   Icon extraction failed. The map still works - markers will use coloured
echo   dots instead of the game's own icons.
echo.
pause & goto :eof

:items_failed
echo.
echo   Item extraction failed. The map still works without it - you will just
echo   have no item markers. Re-run this file to try again.
echo.
pause & goto :eof
